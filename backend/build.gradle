import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.17.0'
    // creates the "./gradlew generateOpenApiDocs" task
    id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
}

openApi {
    outputDir.set(file("$buildDir/"))
}

group = "com.example"
version = "0.0.1-SNAPSHOT"
description = "Demo project for using open api and related tools."

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // core
    implementation "org.springframework.boot:spring-boot-starter-validation"
    // validation
    implementation "org.springframework.boot:spring-boot-starter-web"
    // openapi
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9"
    // lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    // test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    // docker
    //developmentOnly "org.springframework.boot:spring-boot-docker-compose"
    // app.yaml
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('generateTsClient', GenerateTask) {
    generatorName.set('typescript-fetch')
    inputSpec.set("$buildDir/openapi.json".toString())
    outputDir.set("$rootDir/../frontend/src/api/generated".toString())
    apiPackage.set('com.example.api')
    modelPackage.set('com.example.model')
    configOptions = [
            supportsES6              : 'true',
            withInterfaces           : 'true',
            useSingleRequestParameter: 'true',
            typescriptThreePlus      : 'true'
    ]
    // Depends on openapi.json generation
    dependsOn generateOpenApiDocs
    // Clean generated dir
    doFirst {
        delete fileTree(dir: outputDir, include: '**/*')
    }
}